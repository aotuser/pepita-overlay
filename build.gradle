buildscript {
    repositories {
        maven { url "https://maven.minecraftforge.net" }

        maven {
            name = "sponge"
            url = "https://repo.spongepowered.org/maven/"
        }
    }
    dependencies {
        classpath "net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT"
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

plugins {
    id "java"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

apply plugin: "net.minecraftforge.gradle.forge"
apply plugin: 'org.spongepowered.mixin'

version = "2.0.1"
group = "pepita_overlay"
archivesBaseName = "pepita_overlay"

sourceCompatibility = targetCompatibility = 1.8
compileJava.options.encoding = "UTF-8"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"
    mappings = "stable_22"
    makeObfSourceJar = false

    clientRunArgs = [ "--username", "sempescocobro" ]
    clientJvmArgs += '-Dfml.coreMods.load=me.sqqo.mixins.MixinLoader'
}

mixin {
    defaultObfuscationEnv "searge"

    add sourceSets.main, "mixins.pepita_overlay.refmap.json"
}

configurations {
    shade
    compile.extendsFrom(shade)
}

repositories {
    maven { url "https://maven.minecraftforge.net" }

    maven {
        name = "sponge"
        url = "https://repo.spongepowered.org/maven/"
    }
}

dependencies {
    implementation group: 'commons-io', name: 'commons-io', version: '2.11.0'
    shade 'org.spongepowered:mixin:0.7.11-SNAPSHOT'
    compileOnly 'org.jetbrains:annotations:24.0.1'
    shade 'com.squareup.okhttp3:okhttp:3.14.9'
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include "mcmod.info"

        expand "version":project.version, "mcversion":project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "mcmod.info"
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

shadowJar {
    configurations = [project.configurations.shade]
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    classifier ""

    relocate 'okhttp3', 'pepita_overlay.shaded.okhttp3'
    relocate 'okio', 'pepita_overlay.shaded.okio'

    manifest {
        attributes(
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                'MixinConfigs': 'mixins.pepita.json'
        )
    }
}

reobf {
    shadowJar {

    }
}